buildscript {
    ext {
        mavenRepos = {
            mavenLocal()
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url "https://repo.grails.org/grails/core" }
            mavenCentral()
        }
    }
    repositories mavenRepos
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'org.grails.plugins:views-gradle:1.2.0'
    }
}

group "com.causecode.plugins"
version "0.5.5"

apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "org.grails.grails-gsp"
apply plugin:'org.grails.plugins.views-json'
apply plugin: 'jacoco'

repositories mavenRepos

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Default dependencies in grails 3.2.0 for plugin
    // While porting to a new grails version, create a blank app and replace the default dependencies with the new ones.
    compile "org.springframework.boot:spring-boot-starter-logging:1.4.0.RELEASE"
    compile "org.springframework.boot:spring-boot-autoconfigure:1.4.0.RELEASE"
    compile "org.grails:grails-core:3.2.0"
    compile "org.springframework.boot:spring-boot-starter-actuator:1.4.0.RELEASE"
    compile "org.springframework.boot:spring-boot-starter-tomcat:1.4.0.RELEASE"
    compile "org.grails:grails-dependencies:3.2.0"
    compile "org.grails:grails-web-boot:3.2.0"
    compile "org.grails.plugins:cache:3.0.3"
    compile "org.grails.plugins:scaffolding:3.2.1"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    // provided "org.codehaus.groovy:groovy-ant"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"

    /*
    * Dependencies which were needed for the application/plugin.
    */
    provided "org.grails.plugins:hibernate5"
    provided "org.hibernate:hibernate-ehcache:5.1.1.Final"
    provided "org.hibernate:hibernate-validator:5.1.3.Final"

    provided "org.grails.plugins:spring-security-core:3.1.1"
    testCompile "org.grails:grails-datastore-test-support:1.0.2-grails-2.4"
    provided "javax.validation:validation-api:1.1.0.Final"

    provided "org.grails.plugins:views-json:1.1.2"
    provided "org.grails.plugins:views-json-templates:1.1.2"
    compile "org.grails.plugins:asynchronous-mail:2.0.0.RC4"
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'nucleus'
                packaging 'jar'
                description 'nucleus is a Grails-Groovy plugin which contains utility methods, classes and endpoints.'
                url 'https://github.com/causecode/nucleus'

                scm {
                    url 'scm:svn:https://github.com/causecode/nucleus'
                    connection 'scm:svn:https://github.com/causecode/nucleus.git'
                    developerConnection 'https://github.com:causecode/nucleus.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'causecode'
                        name 'CauseCode Technologies'
                        email 'bootstrap@causecode.com'
                    }
                }
            }
        }
    }
}

jacoco {
    toolVersion = '0.7.7.201606060606'
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/jacoco/jacocoHtml/"
        xml.destination "${buildDir}/jacoco/jacocoXml/jacoco.xml"
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/NucleusTagLib*/**',
                            '**/RenderTagLib*/**',
                            '**/NucleusBootStrap*/**',
                            '**/NucleusGrailsPlugin*/**',
                            '**/NucleusUrlMappings*/**',
                            '**/UrlMappings*/**',
                            '**/CustomValidationErrorMarshaller*/**',
                            '**/SitemapMarshaller*/**',
                            '**/Hashids*/**',
                            '**/RestfulController*/**',
                            '**/BaseController*/**',
                            '**/GenericEnhancers*/**',
                            '**/NucleusDevBootStrap*/**'
                    ])
        })
    }

    executionData test
    dependsOn test
}

jar {
    exclude('**/nucleus/UrlMappings*')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
